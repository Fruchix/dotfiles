MEAN() {
    awk '
    /^[[:space:]]*$/ { next }

    !/^[[:space:]]*-?[0-9]+(\.[0-9]+)?[[:space:]]*$/ {
        print "Error: non-numeric input \"" $0 "\"" > "/dev/stderr"
        exit 1
    }

    {
        x = $1
        sum += x
        n++
    }

    END {
        if (!n) {
            print "Error: no numeric input" > "/dev/stderr"
            exit 1
        }
        print sum / n
    }'
}

SUM() {
    awk '
    /^[[:space:]]*$/ { next }

    !/^[[:space:]]*-?[0-9]+(\.[0-9]+)?[[:space:]]*$/ {
        print "Error: non-numeric input \"" $0 "\"" > "/dev/stderr"
        exit 1
    }

    {
        x = $1
        sum += x
        n++
    }

    END {
        if (!n) {
            print "Error: no numeric input" > "/dev/stderr"
            exit 1
        }
        print sum
    }'
}


MAX() {
    awk '
    /^[[:space:]]*$/ { next }   # skip blank lines

    # accept integers or decimals, optional sign
    !/^[[:space:]]*-?[0-9]+(\.[0-9]+)?[[:space:]]*$/ {
        print "Error: non-numeric input \"" $0 "\"" > "/dev/stderr"
        exit 1
    }

    !seen {
        max = $1
        seen = 1
        next
    }

    $1 > max { max = $1 }

    END {
        if (seen)
            print max
        else {
            print "Error: no numeric input" > "/dev/stderr"
            exit 1
        }
    }'
}

# awk 'NR==1 {min=$1} $1<min {min=$1} END {print min}'
MIN() {
    awk '
    /^[[:space:]]*$/ { next }   # skip blank lines

    # accept integers or decimals, optional sign
    !/^[[:space:]]*-?[0-9]+(\.[0-9]+)?[[:space:]]*$/ {
        print "Error: non-numeric input \"" $0 "\"" > "/dev/stderr"
        exit 1
    }

    !seen {
        min = $1
        seen = 1
        next
    }

    $1 < min { min = $1 }

    END {
        if (seen) {
            print min
        } else {
            print "Error: no numeric input" > "/dev/stderr"
            exit 1
        }
    }'
}

STAT() {
    awk '
    /^[[:space:]]*$/ { next }   # skip blank lines

    # validate numeric input (int or decimal, optional sign)
    !/^[[:space:]]*-?[0-9]+(\.[0-9]+)?[[:space:]]*$/ {
        print "Error: non-numeric input \"" $0 "\"" > "/dev/stderr"
        exit 1
    }

    {
        x = $1

        sum += x
        sumsq += x * x
        n++

        if (!seen) {
            min = max = x
            seen = 1
        } else {
            if (x < min) min = x
            if (x > max) max = x
        }
    }

    END {
        if (!seen) {
            print "Error: no numeric input" > "/dev/stderr"
            exit 1
        }

        mean = sum / n
        variance = (sumsq / n) - (mean * mean)

        # guard against tiny negative due to FP rounding
        if (variance < 0) variance = 0

        stddev = sqrt(variance)

        print "COUNT=" n
        print "SUM=" sum
        print "MEAN=" mean
        print "MIN=" min
        print "MAX=" max
        print "STDDEV=" stddev
    }'
}