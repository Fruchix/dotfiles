"    silent !mkdir $XDG_CACHE_HOME/vim/{swapfiles,backup} -p > /dev/null 2>&1
"    set directory=$XDG_CACHE_HOME/vim/swapfiles//
"    set backupdir=$XDG_CACHE_HOME/vim/backup//
"    set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
"    set packpath=$XDG_CONFIG_HOME/vim/
"    set runtimepath^=$XDG_CONFIG_HOME/vim
"    set runtimepath+=$XDG_CONFIG_HOME/vim/after,
"                \$XDG_CONFIG_HOME/vim/ftplugin,
"                \$XDG_CONFIG_HOME/vim/spell/
"    let $MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc"
"endif
" With bashrc line: export VIMINIT='let $MYVIMRC="~/.config/vim/vimrc" | source $MYVIMRC'


" Set appropriate folders
if has('unix') || has ('mac') || has('macunix')
    set packpath^=$XDG_CONFIG_HOME/vim
    set runtimepath^=$XDG_CONFIG_HOME/vim
    set runtimepath+=$XDG_CONFIG_HOME/vim/after,
                \$XDG_CONFIG_HOME/vim/ftplugin,
                \$XDG_CONFIG_HOME/vim/spell/
    let $MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc"
endif


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Global config
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Sets how many lines of history VIM has to remember
set history=500

" Enable filetype plugins
filetype plugin on
filetype indent on

" Set to auto read when a file is changed from the outside
set autoread
au FocusGained,BufEnter * silent! checktime

" Leader definition
nnoremap <SPACE> <Nop>
let mapleader=" "

" Fast saving
nmap <leader>w :w!<cr>

" :W sudo saves the file
" (useful for handling the permission-denied error)
command! W execute 'w !sudo tee % > /dev/null' <bar> edit!


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugins
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let s:plug_path = expand('$XDG_CONFIG_HOME/vim/autoload/plug.vim')

if !filereadable(s:plug_path)
    echo "Installing vim-plug..."
    silent execute '!curl -fLo ' . s:plug_path . ' --create-dirs ' .
        \ 'https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
    
    " Auto-install plugins on first launch
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin()

Plug 'catppuccin/vim', { 'as': 'catppuccin' }
Plug 'airblade/vim-gitgutter'
Plug 'itchyny/lightline.vim'
Plug 'preservim/nerdtree'
Plug 'tpope/vim-commentary'
Plug 'dense-analysis/ale'
Plug 'prabirshrestha/vim-lsp'   " Use language servers

" Plug 'vim-scripts/AutoComplPop' " Automatically open the completion menu when typing

call plug#end()

""" Theme plugin
try
    colorscheme catppuccin_frappe
catch
endtry

set background=dark
set termguicolors

" Apply catppuccin theme to status line
let g:lightline = {'colorscheme': 'catppuccin_frappe'}


""" NERDTree plugin
nnoremap <leader>e :NERDTreeToggle<CR>
" Exit Vim if NERDTree is the only window remaining in the only tab.
autocmd BufEnter * if tabpagenr('$') == 1 && winnr('$') == 1 && exists('b:NERDTree') && b:NERDTree.isTabTree() | call feedkeys(":quit\<CR>:\<BS>") | endif

" Close NERDTree when opening a file
let NERDTreeQuitOnOpen=1


let NERDTreeCustomOpenArgs = {'where': 'p', 'keepAndPrev': 1}
autocmd FileType nerdtree nmap <buffer> <Right> o
autocmd FileType nerdtree nmap <buffer> <Left>  x


""" Git gutter plugin
" Force real time refreshes
let g:gitgutter_realtime = 1
let g:gitgutter_eager = 1
set updatetime=150

let g:gitgutter_sign_added = '▎'
let g:gitgutter_sign_modified = '▎'
let g:gitgutter_sign_removed = '＿'
let g:gitgutter_sign_removed_first_line = '▔'
let g:gitgutter_sign_modified_removed = '▋'


""" Commentary plugin
vnoremap <leader>c  <Plug>Commentary
nnoremap <leader>c <Plug>CommentaryLine


""" ALE Plugin (syntax highlighting, linting, ...)
" Disable completion as it is done by vim-lsp
let g:ale_completion_enabled = 0
let g:ale_completion_autoimport = 0


""" Vim-lsp plugin
" Disable diagnostic as this is done by ALE
let g:lsp_diagnostics_enabled = 0

if executable('bash-language-server')
  au User lsp_setup call lsp#register_server({
        \ 'name': 'bash-language-server',
        \ 'cmd': {server_info->['bash-language-server', 'start']},
        \ 'allowlist': ['sh', 'bash'],
        \ })
endif

function! s:on_lsp_buffer_enabled() abort
    setlocal omnifunc=lsp#complete
    setlocal signcolumn=yes
    if exists('+tagfunc') | setlocal tagfunc=lsp#tagfunc | endif
    " nmap <buffer> gd <plug>(lsp-definition)
    " nmap <buffer> gs <plug>(lsp-document-symbol-search)
    " nmap <buffer> gS <plug>(lsp-workspace-symbol-search)
    " nmap <buffer> gr <plug>(lsp-references)
    " nmap <buffer> gi <plug>(lsp-implementation)
    " nmap <buffer> gt <plug>(lsp-type-definition)
    " nmap <buffer> <leader>rn <plug>(lsp-rename)
    " nmap <buffer> [g <plug>(lsp-previous-diagnostic)
    " nmap <buffer> ]g <plug>(lsp-next-diagnostic)
    nmap <buffer> K <plug>(lsp-hover)
    " nnoremap <buffer> <expr><c-f> lsp#scroll(+4)
    " nnoremap <buffer> <expr><c-d> lsp#scroll(-4)

    let g:lsp_format_sync_timeout = 1000
    autocmd! BufWritePre *.rs,*.go call execute('LspDocumentFormatSync')
endfunction

augroup lsp_install
    au!
    " call s:on_lsp_buffer_enabled only for languages that has the server registered.
    autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END

let g:lsp_async_completion = 1
let g:lsp_documentation_float = 1
let g:lsp_completion_documentation_enabled = 1


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Interface
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Display line numbers
set number

" Always 7 visible lines under and above the cursor
set so=7

" Always show current position
set ruler

" Height of the command bar
set cmdheight=1

" A buffer becomes hidden when it is abandoned
set hid

" Avoid garbled characters in Chinese language windows OS
let $LANG='en'
set langmenu=en
source $VIMRUNTIME/delmenu.vim
source $VIMRUNTIME/menu.vim

" Turn on the Wild menu
set wildmenu

" Ignore compiled files
set wildignore=*.o,*~,*.pyc
if has("win16") || has("win32")
    set wildignore+=.git\*,.hg\*,.svn\*
else
    set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/.DS_Store
endif

" Configure backspace so it acts as it should act
set backspace=eol,start,indent
set whichwrap+=<,>,h,l

" Search options
set ignorecase
set smartcase " try to be smart
set hlsearch " Highlight results
set incsearch " Makes search act like search in modern browsers

" Don't redraw while executing macros (good performance config)
set lazyredraw

" For regular expressions turn magic on
set magic

" Show matching brackets when text indicator is over them
set showmatch

" No annoying sound on errors
set noerrorbells
set novisualbell
set t_vb=
set tm=500

" Properly disable sound on errors on MacVim
if has("gui_macvim")
    autocmd GUIEnter * set vb t_vb=
endif

" No extra margin on the left
set foldcolumn=0
set signcolumn=yes


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Colors and Fonts
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Enable syntax highlighting
syntax enable

" Set regular expression engine automatically
set regexpengine=0

" Enable 256 colors palette in Gnome Terminal
if $COLORTERM == 'gnome-terminal'
    set t_Co=256
endif

" Set extra options when running in GUI mode
if has("gui_running")
    set guioptions-=T
    set guioptions-=e
    set t_Co=256
    set guitablabel=%M\ %t
endif

" Set utf8 as standard encoding and en_US as the standard language
set encoding=utf8

" Use Unix as the standard file type
set ffs=unix,dos,mac


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Files, backups and undo
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Turn backup off, since most stuff is in SVN, git etc. anyway...
set nobackup
set nowb
set noswapfile


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Text, tab and indent related
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Use spaces instead of tabs
set expandtab

" Be smart when using tabs ;)
set smarttab

" 1 tab == 4 spaces
set shiftwidth=4
set tabstop=4

" Linebreak on 500 characters
set lbr
set tw=500

set ai "Auto indent
set si "Smart indent
set wrap "Wrap lines


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Moving around, tabs, windows and buffers
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Smart way to move between windows
map <C-j> <C-W>j
map <C-k> <C-W>k
map <C-h> <C-W>h
map <C-l> <C-W>l

" Close the current buffer
" map <leader>bd :Bclose<cr>:tabclose<cr>gT

" Close all the buffers
" map <leader>ba :bufdo bd<cr>

" map <leader>l :bnext<cr>
" map <leader>h :bprevious<cr>

" Useful mappings for managing tabs
map <leader>tn :tabnew<cr>
map <leader>to :tabonly<cr>
map <leader>tc :tabclose<cr>
map <leader>tm :tabmove

" Navigating throught tabs
noremap <leader><Right> :tabnext<cr>
noremap <leader><Left> :tabprevious<cr>
noremap <leader>& 1gt
noremap <leader>é 2gt
noremap <leader>" 3gt
noremap <leader>' 4gt
noremap <leader>( 5gt
noremap <leader>§ 6gt
noremap <leader>è 7gt
noremap <leader>! 8gt
noremap <leader>ç 9gt

" Let 'tl' toggle between this and the last accessed tab
" let g:lasttab = 1
" nmap <leader>tl :exe "tabn ".g:lasttab<CR>
" au TabLeave * let g:lasttab = tabpagenr()


" Opens a new tab with the current buffer's path
" Super useful when editing files in the same directory
" map <leader>te :tabedit <C-r>=escape(expand("%:p:h"), " ")<cr>/

" Switch CWD to the directory of the open buffer
" map <leader>cd :cd %:p:h<cr>:pwd<cr>

" Specify the behavior when switching between buffers
try
  set switchbuf=useopen,usetab,newtab
  set stal=2
catch
endtry

" Return to last edit position when opening files (You want this!)
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif


""""""""""""""""""""""""""""""
" Status line
""""""""""""""""""""""""""""""
" Always show the status line
set laststatus=2

" Format the status line
set statusline=\ %{HasPaste()}%F%m%r%h\ %w\ \ CWD:\ %r%{getcwd()}%h\ \ \ Line:\ %l\ \ Column:\ %c


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Editing mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" A word is only composed of [a-zA-Z0-9_], and characters with accents,
" so moving by word stops on punctuation and other special characters
set iskeyword=@,48-57,_,192-255

" Display completion menu
set completeopt=menu,menuone,preview,noselect,noinsert
" Display less info when using autocompletion
set shortmess+=c

" Cycle through autocompletion options using tab and Shift+Tab
inoremap <expr> <Tab>   pumvisible() ? "\<C-n>" : "\<Tab>"
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"

" Hack to trigger autocompletion when typing
function! OpenCompletion()
    if !pumvisible() && ((v:char >= 'a' && v:char <= 'z') || (v:char >= 'A' && v:char <= 'Z'))
        call feedkeys("\<C-x>\<C-o>", "n")
    endif
endfunction
autocmd InsertCharPre * call OpenCompletion()

" Start selection with Shift + arrows, in whatever mode
" Start selection if not already in visual mode and extend
nnoremap <S-Left>  v<Left>
nnoremap <S-Right> v<Right>
nnoremap <S-Up>    v<Up>
nnoremap <S-Down>  v<Down>
inoremap <S-Left> <Esc><Right>v<Left>
inoremap <S-Right> <Esc><Right>v<Right>
inoremap <S-Up> <Esc><Right>v<Up>
inoremap <S-Down> <Esc><Right>v<Down>
" Extend selection if already in visual mode
vnoremap <S-Left>  <Left>
vnoremap <S-Right> <Right>
vnoremap <S-Up>    <Up>
vnoremap <S-Down>  <Down>

" Similarly, use Alt + Shift + arrows to select by word
nnoremap <M-S-Left>  vb<Left>
nnoremap <M-S-Right> vw<Right>
inoremap <M-S-Left> <Esc>vb<Left>
inoremap <M-S-Right> <Esc>vw<Right>

" Disable hightlight using leader + Escape
nnoremap <silent> <leader>h :nohlsearch<CR>

" Custom search and replace bidings
" Pressing / in visual mode searches for selection
" vnoremap <silent> / :<C-u>call VisualSelection('', '')<CR>?<C-R>=@/<CR><CR>
vnoremap <silent> / y/\V<C-R>=escape(@", '/\')<CR><CR>
" Pressing leader + r triggers replacement of all those patterns
vnoremap <leader>r :<C-u>call VisualSelection('replace', '')<CR>

" Use alt+j/k to move lines up and down
nnoremap <M-j> :move .+1<CR>==
nnoremap <M-k> :move .-2<CR>==
inoremap <M-j> <Esc>:move .+1<CR>==gi
inoremap <M-k> <Esc>:move .-2<CR>==gi
vnoremap <M-j> :move '>+1<CR>gv=gv
vnoremap <M-k> :move '<-2<CR>gv=gv

" Use Alt+Up/Down to move lines up and down
nnoremap <M-Down> :move .+1<CR>==
nnoremap <M-Up> :move .-2<CR>==
inoremap <M-Down> <Esc>:move .+1<CR>==gi
inoremap <M-Up> <Esc>:move .-2<CR>==gi
vnoremap <M-Down> :move '>+1<CR>gv=gv
vnoremap <M-Up> :move '<-2<CR>gv=gv

" Use Alt+Left/Right to move one word at a time
nnoremap <M-Left>  b
nnoremap <M-Right> w
inoremap <M-Left>  <C-o>b
inoremap <M-Right> <C-o>w
vnoremap <M-Left>  b
vnoremap <M-Right> w
" Iterm2 sends alt+right as Esc+f
nnoremap <Esc>f w
inoremap <Esc>f <C-o>w
vnoremap <Esc>f w
" Iterm2 sends alt+left as Esc+b
nnoremap <Esc>b b
inoremap <Esc>b <C-o>b
vnoremap <Esc>b b

" Use Tab and  Maj+Tab to indent and un-indent
nnoremap <Tab> >>
nnoremap <S-Tab> <<
vnoremap <Tab> >gv
vnoremap <S-Tab> <gv

" Surround Mappings
xnoremap <silent> " :call Surround('"', '"')<CR>
xnoremap <silent> ' :call Surround("'", "'")<CR>
xnoremap <silent> ( :call Surround('(', ')')<CR>
xnoremap <silent> [ :call Surround('[', ']')<CR>
xnoremap <silent> { :call Surround('{', '}')<CR>
xnoremap <silent> < :call Surround('<', '>')<CR>

" Delete following word with Meta+D
nnoremap <M-d> dw
inoremap <M-d> <C-o>dw


" Remap VIM 0 to first non-blank character
map 0 ^

" Delete trailing white space on save, useful for some filetypes ;)
fun! CleanExtraSpaces()
    let save_cursor = getpos(".")
    let old_query = getreg('/')
    silent! %s/\s\+$//e
    call setpos('.', save_cursor)
    call setreg('/', old_query)
endfun

if has("autocmd")
    autocmd BufWritePre *.txt,*.js,*.py,*.wiki,*.sh,*.coffee :call CleanExtraSpaces()
endif


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Spell checking
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Pressing ,ss will toggle and untoggle spell checking
map <leader>ss :setlocal spell!<cr>

" Shortcuts using <leader>
map <leader>sn ]s
map <leader>sp [s
map <leader>sa zg
map <leader>s? z=


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Misc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remove the Windows ^M - when the encodings gets messed up
noremap <Leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

" Quickly open a buffer for scribble
map <leader>q :e ~/buffer<cr>

" Quickly open a markdown buffer for scribble
map <leader>x :e ~/buffer.md<cr>

" Toggle paste mode on and off
map <leader>pp :setlocal paste!<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Helper functions
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Returns true if paste mode is enabled
function! HasPaste()
    if &paste
        return 'PASTE MODE  '
    endif
    return ''
endfunction

" Don't close window, when deleting a buffer
command! Bclose call <SID>BufcloseCloseIt()
function! <SID>BufcloseCloseIt()
    let l:currentBufNum = bufnr("%")
    let l:alternateBufNum = bufnr("#")

    if buflisted(l:alternateBufNum)
        buffer #
    else
        bnext
    endif

    if bufnr("%") == l:currentBufNum
        new
    endif

    if buflisted(l:currentBufNum)
        execute("bdelete! ".l:currentBufNum)
    endif
endfunction

function! CmdLine(str)
    call feedkeys(":" . a:str)
endfunction

" Helper function to search and replace the visual selection"
function! VisualSelection(direction, extra_filter) range
    let l:saved_reg = @"
    execute "normal! vgvy"

    let l:pattern = escape(@", "\\/.*'$^~[]")
    let l:pattern = substitute(l:pattern, "\n$", "", "")

    if a:direction == 'gv'
        call CmdLine("Ack '" . l:pattern . "' " )
    elseif a:direction == 'replace'
        call CmdLine("%s" . '/'. l:pattern . '/' . l:pattern . '/gc' . "\<Left>\<Left>\<Left>")
        " call CmdLine("%s" . '/'. l:pattern . '/')
    endif

    let @/ = l:pattern
    let @" = l:saved_reg
endfunction

" Vanilla Surround Function
function! Surround(char_start, char_end)
    " Cut the text into the unnamed register "
    normal! gvc

    " Get the content from the register
    let l:content = @"

    " Trim any trailing newline if the selection was Linewise (V)
    " This fixes the 'extra spacing' or broken line issue.
    if l:content =~ "\n$"
        let l:content = substitute(l:content, '\n$', '', '')
    endif

    " Construct the new string
    let l:new_text = a:char_start . l:content . a:char_end

    " Paste it back literally (P prevents extra indentation logic)
    let @" = l:new_text
    normal! P
endfunction
